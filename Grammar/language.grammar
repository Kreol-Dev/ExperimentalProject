%header%
GRAMMARTYPE = "LL"
AUTHOR = "Bulanenkov Vitaly - Moonmoth Studio"
VERSION = "0.2"
%tokens%
WHITESPACE    = <<[ \t\n\r]+>> %ignore%
OPEN_TABLE = "{"
CLOSE_TABLE = "}"
DOT = "."
DELIMITER = ","
OPEN_PARENT = "("
CLOSE_PARENT = ")"

AND = "and"
OR = "or"
NOT = "not"
EQUALS = "="
NOTEQUALS = "/="
ADD = "+"
SUB = "-"
DIV = "/"
MUL = "*"
MORE = ">"
LESS = "<"
MOREOREQUALS = ">="
LESSOREQUALS = "<="

TRUE = "yes"
FALSE = "no"
IDENTIFIER = <<([a-z]|_)+>>
NUMBER = <<[0-9]+>>
STRING = <<[\"]([^"\\]|\\.)*[\"]>>



%productions%



ROOT = (IDENTIFIER EQUALS CONTEXT)+;
SCOPE = IDORCALL (DOT IDORCALL)*;
OPERATOR = IDORCALL EQUALS CONTEXT;
CONTEXT = OPEN_TABLE OPSORATOMS CLOSE_TABLE | ATOM | SCOPE;
OPSORATOMS = IDORCALL (OPSORATOMSTAILS)? | ATOM (ATOMORIDORCALL)*;
OPSORATOMSTAILS = (ATOMORIDORCALL)+| EQUALS CONTEXT (OPERATOR)*;

IDORCALL = IDENTIFIER (OPEN_PARENT (EXPRESSION (DELIMITER EXPRESSION)*)? CLOSE_PARENT)?;

ATOM = NUMBER | TRUE | FALSE | STRING;
ATOMORIDORCALL = ATOM | IDORCALL;
FACTOR = (SUB | NOT)? ATOMORIDORCALL | (SUB | NOT)? OPEN_PARENT EXPRESSION CLOSE_PARENT;
MULTTERM = FACTOR [MULTTERMTAIL];
MULTTERMTAIL = MUL MULTTERM | DIV MULTTERM;

ADDITTERM = MULTTERM [ADDITTERMTAIL];
ADDITTERMTAIL = ADD ADDITTERM | SUB ADDITTERM;

RELATTERM = ADDITTERM [RELATTERMTAIL];
RELATTERMTAIL = MORE RELATTERM | LESS RELATTERM | MOREOREQUALS RELATTERM | LESSOREQUALS RELATTERM;

EQTERM = RELATTERM [EQTERMTAIL];
EQTERMTAIL = EQUALS EQTERM;

ANDTERM = EQTERM [ANDTERMTAIL];
ANDTERMTAIL = AND ANDTERM;

ORTERM = ANDTERM [ORTERMTAIL];
ORTERMTAIL = OR ORTERM;

EXPRESSION = ORTERM;