%header%
GRAMMARTYPE = "LL"
AUTHOR = "Bulanenkov Vitaly - Moonmoth Studio"
VERSION = "0.2"
%tokens%
WHITESPACE    = <<[ \t\n\r]+>> %ignore%
OPEN_TABLE = "{"
CLOSE_TABLE = "}"
DELIMITER = ","
OPEN_PARENT = "("
CLOSE_PARENT = ")"

AND = "and"
OR = "or"
NOT = "not"
EQUALS = "="
NOTEQUALS = "/="
ADD = "+"
SUB = "-"
DIV = "/"
MUL = "*"
MORE = ">"
LESS = "<"
MOREOREQUALS = ">="
LESSOREQUALS = "<="

TRUE = "yes"
FALSE = "no"
IDENTIFIER = <<([a-z]|[A-Z]|_)+>>
NUMBER = <<0|[1-9]|[1-9][0-9]*>>
STRING = <<[\"]([^"\\]|\\.)*[\"]>>
DOT = "."



%productions%
Root = Operator+;
Scope = IdentifierOrCall (DOT IdentifierOrCall)*;
FuncScope = IDENTIFIER (DOT IDENTIFIER)*;
IdentifierOrCall = IDENTIFIER (OPEN_PARENT (CallArgs)? CLOSE_PARENT)?;
CallArgs = Expression (DELIMITER Expression)*;
Operator = IdentifierOrCall EQUALS Context;
Context = OPEN_TABLE List CLOSE_TABLE | Expression;
List = (IdentifierOrCall (EQUALS Context)? | Atom | OPEN_TABLE List CLOSE_TABLE)+;

Atom = NUMBER | TRUE | FALSE | STRING;
Factor = (SUB | NOT)? ((Atom | Scope) | OPEN_PARENT Expression CLOSE_PARENT);
MulTerm = Factor (MUL Factor | DIV Factor)*;

AddTerm = MulTerm (ADD MulTerm | SUB MulTerm)*;

RelateTerm = AddTerm (MORE AddTerm | LESS AddTerm | MOREOREQUALS AddTerm | LESSOREQUALS AddTerm)*;

EqTerm = RelateTerm (EQUALS RelateTerm)*;

AndTerm = EqTerm (AND EqTerm)*;

OrTerm = AndTerm (OR AndTerm)*;

Expression = OrTerm;

